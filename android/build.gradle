apply plugin: 'com.android.library'
import groovy.json.JsonSlurper

def getNpmVersion() {
    def packageJsonFile = file('../package.json')
    def packageJson = new JsonSlurper().parseText(packageJsonFile.text)
    return packageJson.version
}

repositories {
    mavenCentral()
    jcenter()
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 26)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 26)

        versionCode 1
        versionName "1.0"

        buildConfigField("String", "RNBRANCH_VERSION", "\"${getNpmVersion()}\"")
    }
}

dependencies {
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'com.facebook.react:react-native:+' // From node_modules
    api 'io.branch.sdk.android:library:5.0.3'
}
